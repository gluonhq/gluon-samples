buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.1.1'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE'
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.13.1"
}

apply plugin: 'base'

configure([project(':webapp-base'), project(':webapp-mobile')]) {
    apply plugin: 'java'
    apply plugin: 'spring-boot'

    repositories {
        jcenter()
    }

    dependencies {
        // Spring Boot
        compile 'org.springframework.boot:spring-boot-starter-web'
        compile 'org.springframework.boot:spring-boot-starter-data-jpa'

        // Spring Cloud Connectors
        compile "org.springframework.cloud:spring-cloud-cloudfoundry-connector:1.2.3.RELEASE"
        compile "org.springframework.cloud:spring-cloud-spring-service-connector:1.2.3.RELEASE"

        // Spring security
        compile "org.springframework.security.oauth:spring-security-oauth"

        // JPA Persistence
        compile 'commons-dbcp:commons-dbcp'
        runtime 'com.h2database:h2'
        runtime "mysql:mysql-connector-java"
        runtime 'postgresql:postgresql:9.1-901-1.jdbc4'

        // JSON
        compile 'org.glassfish:javax.json:1.0.4'

        // Webjars
        compile "org.webjars:bootstrap:3.1.1"
        compile "org.webjars:jquery:2.1.0-2"
    }

    jar {
        // omit the version from the jar file name
        version = ''
    }
}

configure([project(':mobileapp')]) {
    apply plugin: 'org.javafxports.jfxmobile'
    applicationName = 'Whiteboard'

    repositories {
        jcenter()
        maven {
            url 'http://nexus.gluonhq.com/nexus/content/repositories/releases'
        }
        mavenLocal()
    }

    dependencies {
        compile 'com.gluonhq:charm-glisten:4.0.1'
        compile 'com.gluonhq:charm-glisten-connect-view:4.0.1'
        compile 'com.gluonhq:connect:1.2.0'
        compileNoRetrolambda 'com.airhacks:afterburner.mfx:1.6.2'
    }

    mainClassName = 'com.gluonhq.cloudlink.sample.whiteboard.Whiteboard'

    jfxmobile {
        downConfig {
            plugins 'display', 'lifecycle', 'statusbar', 'storage'
        }
        android {
            manifest = 'src/android/AndroidManifest.xml'
        }
        ios {
            infoPList = file('src/main/ios/Default-Info.plist')
            forceLinkClasses = [
                    'com.gluonhq.cloudlink.sample.whiteboard.**.*',
                    'com.gluonhq.**.*',
                    'javax.annotations.**.*',
                    'javax.inject.**.*',
                    'javax.json.**.*',
                    'org.glassfish.json.**.*'
            ]
        }
    }
}

license {
    skipExistingHeaders
    mapping {
        fxml = 'XML_STYLE'
    }
}

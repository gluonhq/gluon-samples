buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.3.6'
    }
}

subprojects {
    repositories {
        jcenter()
    }
}

configure([project(':server')]) {
    apply plugin: 'war'
    apply plugin: 'application'

    repositories {
        mavenCentral()
        maven {
            url 'https://repo.eclipse.org/content/groups/releases/'
        }
    }

    configurations {
        payara
    }

    dependencies {
        providedCompile 'javax:javaee-api:7.0'

        // Gluon CloudLink Enterprise SDK
        compile 'com.gluonhq:cloudlink-enterprise-sdk-javaee:1.2.0'
        runtime 'org.eclipse:yasson:1.0'

        // jersey framework
        providedCompile 'org.glassfish.jersey.containers:jersey-container-servlet:2.22.2'
        compile 'org.glassfish.jersey.ext:jersey-mvc:2.22.2'
        compile 'org.glassfish.jersey.ext:jersey-mvc-freemarker:2.22.2'

        // Webjars
        runtime "org.webjars:bootstrap:3.3.7-1"
        runtime "org.webjars:jquery:3.1.1"

        payara 'fish.payara.extras:payara-micro:4.1.2.172'
        payara 'org.eclipse:yasson:1.0'
        payara 'org.glassfish:javax.json:1.1'
    }

    war {
        baseName = 'motd-server'
        version = null
    }

    mainClassName = 'fish.payara.micro.PayaraMicro'
    run {
        classpath = configurations.payara
        args '--deploy', war.archivePath.absolutePath
    }
    tasks.run.dependsOn tasks.war
}

configure([project(':client')]) {
    apply plugin: 'org.javafxports.jfxmobile'

    repositories {
        maven {
            url 'http://nexus.gluonhq.com/nexus/content/repositories/releases/'
        }
    }

    dependencies {
        compile 'com.gluonhq:glisten-afterburner:1.4.0'
        compile 'com.gluonhq:charm-cloudlink-client:4.3.5'
    }

    mainClassName = 'com.gluonhq.javaee.motd.client.MessageOfTheDay'

    jfxmobile {
        downConfig {
            version = '3.8.0'
            plugins 'display', 'lifecycle', 'statusbar', 'storage'
        }
        android {
            manifest = 'src/android/AndroidManifest.xml'
        }
        ios {
            infoPList = file('src/ios/Default-Info.plist')
            forceLinkClasses = [
                    'com.gluonhq.javaee.motd.client.**.*',
                    'com.gluonhq.**.*',
                    'javax.annotations.**.*',
                    'javax.inject.**.*',
                    'javax.json.**.*',
                    'org.glassfish.json.**.*'
            ]
        }
    }
}


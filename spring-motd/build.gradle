buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.3.5'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.3.RELEASE'
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.13.1"
}

apply plugin: 'base'


subprojects {
    apply plugin: 'com.github.hierynomus.license'


    repositories {
        jcenter()
        maven {
            url 'http://nexus.gluonhq.com/nexus/content/repositories/releases'
        }
    }

    license {
        header = rootProject.file('LICENSE')
        skipExistingHeaders = false
        mapping {
            java = 'SLASHSTAR_STYLE'
            fxml = 'XML_STYLE'
        }
        excludes(['**/*.yml', '**/*.png', '**/*.properties'])
    }
}

configure([project(':server')]) {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8


    dependencies {

        // Gluon CloudLink Enterprise Client
        compile 'com.gluonhq:cloudlink-client-enterprise-feign:1.0.0'

        // Spring Boot
        compile 'org.springframework.boot:spring-boot-starter-web'

        // Webjars
        compile "org.webjars:bootstrap:3.3.7-1"
        compile "org.webjars:jquery:3.1.1"
    }

    jar {
        // omit the version from the jar file name
        version = ''
    }

}

configure([project(':client')]) {


    apply plugin: 'org.javafxports.jfxmobile'
    applicationName = 'MessageOfTheDay'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8


    dependencies {
        compile 'com.gluonhq:glisten-afterburner:1.2.0'
        compile 'com.gluonhq:charm-cloudlink-client:4.3.5'
    }

    mainClassName = 'com.gluonhq.spring.motd.client.MessageOfTheDay'

    jfxmobile {
        downConfig {
            version = '3.3.0'
            plugins 'display', 'lifecycle', 'statusbar', 'storage'
        }
        android {
            manifest = 'src/android/AndroidManifest.xml'
        }
        ios {
            infoPList = file('src/ios/Default-Info.plist')
            forceLinkClasses = [
                    'com.gluonhq.spring.motd.**.*',
                    'com.gluonhq.**.*',
                    'javax.annotations.**.*',
                    'javax.inject.**.*',
                    'javax.json.**.*',
                    'org.glassfish.json.**.*'
            ]
        }
    }
}


